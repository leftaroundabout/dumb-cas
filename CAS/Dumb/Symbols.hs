-- |
-- Module      : CAS.Dumb.Symbols
-- Copyright   : (c) Justus Sagem√ºller 2017
-- License     : GPL v3
-- 
-- Maintainer  : (@) jsag $ hvl.no
-- Stability   : experimental
-- Portability : portable
-- 

{-# LANGUAGE PatternSynonyms           #-}
{-# LANGUAGE FlexibleInstances         #-}
{-# LANGUAGE FlexibleContexts          #-}
{-# LANGUAGE UndecidableInstances      #-}
{-# LANGUAGE OverloadedStrings         #-}
{-# LANGUAGE ConstraintKinds           #-}
{-# LANGUAGE TypeFamilies              #-}
{-# LANGUAGE ScopedTypeVariables       #-}
{-# LANGUAGE UnicodeSyntax             #-}

module CAS.Dumb.Symbols where

import CAS.Dumb.Tree

import Data.Monoid
import qualified Language.Haskell.TH.Syntax as Hs

import Control.Arrow

import Data.String (IsString)

import GHC.Exts (Constraint)
import GHC.Stack (HasCallStack)

import Data.Ratio (denominator, numerator)
import Numeric.Literals.Decimal


data SymbolD œÉ c = NatSymbol !Integer
                 | PrimitiveSymbol Char
                 | StringSymbol c

data Infix s = Infix {
    symbolFixity :: !Hs.Fixity
  , infixSymbox :: !s
  }

instance Eq s => Eq (Infix s) where
  Infix _ o == Infix _ p = o==p

type family SpecialEncapsulation s

data Encapsulation s = Encapsulation {
      needInnerParens, haveOuterparens :: !Bool
    , leftEncaps, rightEncaps :: !s
    }
  | SpecialEncapsulation (SpecialEncapsulation s)

instance Eq (Encapsulation String) where
  Encapsulation _ _ l r == Encapsulation _ _ l' r'
         = dropParens (reverse l) r == dropParens (reverse l') r'
   where dropParens ('(':lr) (')':rr) = dropParens lr rr
         dropParens (' ':lr) rr = dropParens lr rr
         dropParens lr (' ':rr) = dropParens lr rr
         dropParens lr rr = (lr,rr)
  SpecialEncapsulation e == SpecialEncapsulation e' = e==e'
  _ == _ = False

type AlgebraExpr œÉ l = CAS (Infix l) (Encapsulation l) (SymbolD œÉ l)
type AlgebraExpr' Œ≥ œÉ l = CAS' Œ≥ (Infix l) (Encapsulation l) (SymbolD œÉ l)
type AlgebraPattern œÉ l = AlgebraExpr' GapId œÉ l

don'tParenthesise :: Monoid s¬π
                  => CAS' Œ≥ (Infix s¬≤) (Encapsulation s¬π) s‚Å∞
                  -> CAS' Œ≥ (Infix s¬≤) (Encapsulation s¬π) s‚Å∞
don'tParenthesise (Symbol s) = Symbol s
don'tParenthesise (Gap Œ≥) = Gap Œ≥
don'tParenthesise (Function (Encapsulation nin _ l r) x)
        = Function (Encapsulation nin True l r) x
don'tParenthesise x = Function (Encapsulation False True mempty mempty) x
      
symbolInfix :: s¬≤ -- ^ The operator we want to describe
  -> CAS' Œ≥ s¬≤ s¬π s‚Å∞ -> CAS' Œ≥ s¬≤ s¬π s‚Å∞ -> CAS' Œ≥ s¬≤ s¬π s‚Å∞
symbolInfix = Operator

symbolFunction :: Monoid s¬π => s¬π
  -> CAS' Œ≥ (Infix s¬≤) (Encapsulation s¬π) s‚Å∞
  -> CAS' Œ≥ (Infix s¬≤) (Encapsulation s¬π) s‚Å∞
symbolFunction f a = Function (Encapsulation True False f mempty) a



data AlgebraicInvEncapsulation
       = Negation | Reciprocal
 deriving (Eq, Show)

type instance SpecialEncapsulation String = AlgebraicInvEncapsulation

instance ‚àÄ œÉ Œ≥ . (SymbolClass œÉ, SCConstraint œÉ String)
          => Num (AlgebraExpr' Œ≥ œÉ String) where
  fromInteger n
   | n<0        = negate . fromInteger $ -n
   | otherwise  = Symbol $ NatSymbol n
  (+) = chainableInfixL (==plusOp) plusOp
   where fcs = fromCharSymbol ([]::[œÉ])
         plusOp = Infix (Hs.Fixity 6 Hs.InfixL) $ fcs '+'
  (*) = chainableInfixL (==mulOp) mulOp
   where fcs = fromCharSymbol ([]::[œÉ])
         mulOp = Infix (Hs.Fixity 7 Hs.InfixL) $ fcs '*'
  abs = symbolFunction "abs "
  signum = symbolFunction "signum "
  negate = Function $ SpecialEncapsulation Negation

instance ‚àÄ œÉ Œ≥ . (SymbolClass œÉ, SCConstraint œÉ String)
          => Fractional (AlgebraExpr' Œ≥ œÉ String) where
  fromRational n = case fromRational n of
     n:%d -> fromIntegral n / fromIntegral d
     nSci -> Symbol (StringSymbol $ show nSci)
  recip = Function $ SpecialEncapsulation Reciprocal

instance ‚àÄ œÉ Œ≥ . (SymbolClass œÉ, SCConstraint œÉ String)
          => Floating (AlgebraExpr' Œ≥ œÉ String) where
  pi = Symbol $ StringSymbol "pi"
  (**) = symbolInfix (Infix (Hs.Fixity 8 Hs.InfixL) "**")
  logBase = symbolInfix (Infix (Hs.Fixity 10 Hs.InfixL) "`logBase`")
  sqrt = symbolFunction $ "sqrt "
  exp = symbolFunction $ "exp "
  log = symbolFunction $ "log "
  sin = symbolFunction $ "sin "
  cos = symbolFunction $ "cos "
  tan = symbolFunction $ "tan "
  asin = symbolFunction $ "asin "
  acos = symbolFunction $ "acos "
  atan = symbolFunction $ "atan "
  sinh = symbolFunction $ "sinh "
  cosh = symbolFunction $ "cosh "
  tanh = symbolFunction $ "tanh "
  asinh = symbolFunction $ "asinh "
  acosh = symbolFunction $ "acosh "
  atanh = symbolFunction $ "atanh "

class ASCIISymbols c where
  fromASCIISymbol :: Char -> c
  toASCIISymbols :: c -> String

instance ASCIISymbols String where
  fromASCIISymbol = pure
  toASCIISymbols = id


class Eq (SpecialEncapsulation c) => RenderableEncapsulations c where
  fixateAlgebraEncaps :: (SymbolClass œÉ, SCConstraint œÉ c)
      => CAS' Œ≥ (Infix c) (Encapsulation c) (SymbolD œÉ c)
                         -> CAS' Œ≥ (Infix c) (Encapsulation c) (SymbolD œÉ c)

instance RenderableEncapsulations String where
  fixateAlgebraEncaps (OperatorChain x
                         ((o,Function (SpecialEncapsulation Œπ) z):ys))
     | (Infix (Hs.Fixity 6 Hs.InfixL) "+", Negation) <- (o,Œπ)
           = case fixateAlgebraEncaps $ OperatorChain x ys of
               x' -> Operator (Infix (Hs.Fixity 6 Hs.InfixL) "-") x' z'
     | (Infix (Hs.Fixity 7 Hs.InfixL) "*", Reciprocal) <- (o,Œπ)
           = case fixateAlgebraEncaps $ OperatorChain x ys of
               x' -> Operator (Infix (Hs.Fixity 7 Hs.InfixL) "/") x' z'
   where z' = fixateAlgebraEncaps z
  fixateAlgebraEncaps (OperatorChain x []) = fixateAlgebraEncaps x
  fixateAlgebraEncaps (OperatorChain x ((o@(Infix (Hs.Fixity _ Hs.InfixL) _), z):ys))
      = Operator o (fixateAlgebraEncaps $ OperatorChain x ys) (fixateAlgebraEncaps z)
  fixateAlgebraEncaps (Operator o x (Function (SpecialEncapsulation Œπ) y))
     | (Infix (Hs.Fixity 6 Hs.InfixL) "+", Negation) <- (o,Œπ)
           = Operator (Infix (Hs.Fixity 6 Hs.InfixL) "-") x' y'
     | (Infix (Hs.Fixity 7 Hs.InfixL) "*", Reciprocal) <- (o,Œπ)
           = Operator (Infix (Hs.Fixity 7 Hs.InfixL) "/") x' y'
   where [x',y'] = fixateAlgebraEncaps<$>[x,y]
  fixateAlgebraEncaps (Function (SpecialEncapsulation Negation) e)
            = Operator (Infix (Hs.Fixity 6 Hs.InfixL) "-")
                (Symbol $ StringSymbol " ") $ fixateAlgebraEncaps e
  fixateAlgebraEncaps (Function (SpecialEncapsulation Reciprocal) e)
            = Operator (Infix (Hs.Fixity 7 Hs.InfixL) "/")
                (Symbol $ NatSymbol 1) $ fixateAlgebraEncaps e
  fixateAlgebraEncaps (Function f e) = Function f $ fixateAlgebraEncaps e
  fixateAlgebraEncaps (Operator o x y)
        = Operator o (fixateAlgebraEncaps x) (fixateAlgebraEncaps y)
  fixateAlgebraEncaps (OperatorChain x‚ÇÄ oys)
        = OperatorChain (fixateAlgebraEncaps x‚ÇÄ) (second fixateAlgebraEncaps <$> oys)
  fixateAlgebraEncaps e = e

type RenderingCombinator œÉ c r
        = Bool        -- ^ Should the result be parenthesised?
       -> Maybe r     -- ^ Left context
       -> SymbolD œÉ c -- ^ Central expression/function/infix to render
       -> Maybe r     -- ^ Right context
       -> r           -- ^ Rendering result

data ContextFixity = AtLHS Hs.Fixity
                   | AtRHS Hs.Fixity
                   | AtFunctionArgument
                   deriving (Eq)

expressionFixity :: AlgebraExpr œÉ c -> Maybe Hs.Fixity
expressionFixity (Symbol _) = Nothing
expressionFixity (Function _ _) = Nothing
expressionFixity (Operator (Infix fxty _) _ _) = Just fxty
expressionFixity (OperatorChain _ ((Infix fxty _,_):_)) = Just fxty
expressionFixity (OperatorChain x‚ÇÄ []) = expressionFixity x‚ÇÄ
expressionFixity (Gap _) = Nothing

renderSymbolExpression :: ‚àÄ œÉ c r . (SymbolClass œÉ, SCConstraint œÉ c, HasCallStack)
         => ContextFixity -> RenderingCombinator œÉ c r
                    -> AlgebraExpr œÉ c -> r
renderSymbolExpression _ œÅ (Symbol s) = œÅ False Nothing s Nothing
renderSymbolExpression ctxt œÅ (Function (Encapsulation needInnerP atomical l r) x)
   = œÅ (not atomical && ctxt==AtFunctionArgument) Nothing (StringSymbol l) . Just
      $ œÅ False (Just $ renderSymbolExpression
                          (if needInnerP then AtFunctionArgument
                                         else AtLHS (Hs.Fixity (-1) Hs.InfixN))
                          œÅ x)
                (StringSymbol r) Nothing
renderSymbolExpression ctxt œÅ (Operator o x y)
    = renderSymbolExpression ctxt œÅ $ OperatorChain x [(o,y)]
renderSymbolExpression ctxt œÅ (OperatorChain x []) = renderSymbolExpression ctxt œÅ x
renderSymbolExpression ctxt œÅ (OperatorChain x ys@(_:_)) = go parens x ys
 where fxty = foldr1 ( \f f' -> if f==f'
                  then f
                  else error "All infixes in an OperatorChain must have the same fixity"
                     ) $ symbolFixity . fst <$> ys
       go parens x [(Infix _ o,y)]
             = œÅ parens (Just $ renderSymbolExpression (AtLHS fxty) œÅ x)
                        (StringSymbol o)
                        (Just $ renderSymbolExpression (AtRHS fxty) œÅ y)
       go parens x ((Infix _ o,y):zs)
             = œÅ parens (Just $ go False x zs)
                        (StringSymbol o)
                        (Just $ renderSymbolExpression (AtRHS fxty) œÅ y)
       parens = case ctxt of
         AtFunctionArgument -> True
         AtLHS (Hs.Fixity pfxty _)         | Hs.Fixity lfxty _ <- fxty
                                           , lfxty < pfxty                      -> True
         AtLHS (Hs.Fixity pfxty Hs.InfixL) | Hs.Fixity lfxty Hs.InfixL <- fxty
                                           , lfxty==pfxty                       -> False
         AtLHS (Hs.Fixity pfxty _)         | Hs.Fixity lfxty _ <- fxty
                                           , lfxty==pfxty                       -> True
         AtLHS _                                                                -> False
         AtRHS (Hs.Fixity pfxty _)         | Hs.Fixity lfxty _ <- fxty
                                           , lfxty < pfxty                      -> True
         AtRHS (Hs.Fixity pfxty Hs.InfixR) | Hs.Fixity lfxty Hs.InfixR <- fxty
                                           , lfxty==pfxty                       -> False
         AtRHS (Hs.Fixity pfxty _)         | Hs.Fixity lfxty _ <- fxty
                                           , lfxty==pfxty                       -> True
         AtRHS _                                                                -> False
renderSymbolExpression _ _ (Function (SpecialEncapsulation _) _) = error
 "`renderSymbolExpression` cannot handle `SpecialEncapsulation`; please pre-process accordingly."


showsPrecASCIISymbol :: (ASCIISymbols c, SymbolClass œÉ, SCConstraint œÉ c)
       => Int -> AlgebraExpr œÉ c -> ShowS
showsPrecASCIISymbol ctxt
      = renderSymbolExpression (AtLHS (Hs.Fixity ctxt Hs.InfixN)) œÅ
 where œÅ dop lctxt (StringSymbol sym) rctxt
           = showParen dop $ maybe id id lctxt . (toASCIISymbols sym++) . maybe id id rctxt
       œÅ dop lctxt (NatSymbol n) rctxt
           = showParen dop $ maybe id id lctxt . shows n . maybe id id rctxt
       œÅ dop lctxt (PrimitiveSymbol c) rctxt
           = showParen dop $ maybe id id lctxt . (c:) . maybe id id rctxt


class UnicodeSymbols c where
  fromUnicodeSymbol :: Char -> c
  toUnicodeSymbols :: c -> String

instance UnicodeSymbols String where
  fromUnicodeSymbol = pure
  toUnicodeSymbols = id


showsPrecUnicodeSymbol :: (UnicodeSymbols c, SymbolClass œÉ, SCConstraint œÉ c)
       => Int -> AlgebraExpr œÉ c -> ShowS
showsPrecUnicodeSymbol ctxt
      = renderSymbolExpression (AtLHS (Hs.Fixity ctxt Hs.InfixN)) œÅ
 where œÅ dop lctxt (StringSymbol sym) rctxt
           = showParen dop $ maybe id id lctxt . (toUnicodeSymbols sym++) . maybe id id rctxt
       œÅ dop lctxt (NatSymbol n) rctxt
           = showParen dop $ maybe id id lctxt . shows n . maybe id id rctxt
       œÅ dop lctxt (PrimitiveSymbol c) rctxt
           = showParen dop $ maybe id id lctxt . (c:) . maybe id id rctxt



class SymbolClass œÉ where
  type SCConstraint œÉ :: * -> Constraint
  fromCharSymbol :: (Functor p, SCConstraint œÉ c) => p œÉ -> Char -> c

normaliseSymbols :: ‚àÄ œÉ c Œ≥ s¬≤ s¬π . (SymbolClass œÉ, SCConstraint œÉ c)
                      => CAS' Œ≥ s¬≤ s¬π (SymbolD œÉ c) -> CAS' Œ≥ s¬≤ s¬π (SymbolD œÉ c)
normaliseSymbols = fmap nmlzSym
 where nmlzSym (PrimitiveSymbol c) = case fromCharSymbol ([]::[œÉ]) of
           fcs -> StringSymbol $ fcs c
       nmlzSym s = s

instance ‚àÄ œÉ c . (SymbolClass œÉ, SCConstraint œÉ c, Eq c) => Eq (SymbolD œÉ c) where
  NatSymbol i == NatSymbol j  = i==j
  StringSymbol x == StringSymbol y  = x==y
  PrimitiveSymbol x == PrimitiveSymbol y  = x==y
  x@(PrimitiveSymbol c) == y  = case fromCharSymbol ([]::[œÉ]) of
            fcs -> StringSymbol (fcs c)==y
  x == y@(PrimitiveSymbol c)  = case fromCharSymbol ([]::[œÉ]) of
            fcs -> x==StringSymbol (fcs c)
  _ == _ = False

infixl 4 %$>
-- | Transform the symbols of an expression, in their underlying representation.
--
-- @
-- (map succ%$> ùëé+ùëù) * ùë•  ‚â°  (ùëè+ùëû) * ùë•
-- @
-- 
--   Note that this can /not/ be used with number literals.
(%$>) :: ‚àÄ œÉ c c' Œ≥ s¬≤ s¬π . (SymbolClass œÉ, SCConstraint œÉ c)
         => (c -> c') -> CAS' Œ≥ s¬≤ s¬π (SymbolD œÉ c) -> CAS' Œ≥ s¬≤ s¬π (SymbolD œÉ c')
f %$> Symbol (PrimitiveSymbol c) = case fromCharSymbol ([]::[œÉ]) of
         fcs -> Symbol . StringSymbol . f $ fcs c
f %$> Symbol (StringSymbol s) = Symbol . StringSymbol $ f s
_ %$> Symbol (NatSymbol _) = error "`%$>` cannot be used with number literals."
f %$> Function g q = Function g $ f %$> q
f %$> Operator o p q = Operator o (f%$>p) (f%$>q)
f %$> OperatorChain p qs = OperatorChain (f%$>p) (second (f%$>)<$>qs)
f %$> Gap Œ≥ = Gap Œ≥



continueExpr :: (Eq l, Monoid l)
     => ( AlgebraExpr' Œ≥ œÉ l -> AlgebraExpr' Œ≥ œÉ l -> AlgebraExpr' Œ≥ œÉ l )
       -- ^ Combinator to use for chaining the new expression to the old ones
     -> ( AlgebraExpr' Œ≥ œÉ l -> AlgebraExpr' Œ≥ œÉ l )
       -- ^ Transformation to apply to the rightmost expression in the previous chain
     -> ( AlgebraExpr' Œ≥ œÉ l -> AlgebraExpr' Œ≥ œÉ l )
       -- ^ Transformation which appends the result.
continueExpr op f = go
 where go (OperatorChain e‚ÇÄ ((eo@(Infix (Hs.Fixity fte _) _), eŒ©):es))
         | fte <= chainingFxty
                    = associativeOperator eo (OperatorChain e‚ÇÄ es) (go eŒ©)
       go e
         | Just (co, fxtyDir) <- chainingOp
              = OperatorChain e [(Infix (Hs.Fixity chainingFxty fxtyDir) co, f e)]
         | otherwise
              = op e $ f e
       (chainingFxty, chainingOp)
                      = case op (Symbol $ StringSymbol mempty)
                                (Symbol $ StringSymbol mempty) of
          OperatorChain _ ((Infix (Hs.Fixity fxty fxtyDir) op, _):_)
            -> (fxty, Just (op, fxtyDir))
          _ -> (-1, Nothing)



infixl 1 &~~!, &~~:

-- | Apply a sequence of pattern-transformations and yield the result
--   concatenated to the original via the corresponding chain-operator.
--   Because only the rightmost expression in a chain is processed,
--   this can be iterated, giving a chain of intermediate results.
--
--   If one of the patterns does not match, this manipulator will raise
--   an error.
(&~~!) :: ( Eq l, Eq (Encapsulation l), SymbolClass œÉ, SCConstraint œÉ l
         , Show (AlgebraExpr œÉ l), Show (AlgebraPattern œÉ l) )
    => AlgebraExpr œÉ l -> [AlgebraPattern œÉ l] -> AlgebraExpr œÉ l
e &~~! [] = e
OperatorChain e‚ÇÄ ((eo@(Infix (Hs.Fixity fte _) _), eŒ©):es)
     &~~! tfms@(OperatorChain p‚ÇÄ [(to@(Infix (Hs.Fixity ftp _) _),p‚ÇÅ)] : _)
   | fte<=ftp   = associativeOperator eo (OperatorChain e‚ÇÄ es) (eŒ©&~~!tfms)
e &~~! tfms@(OperatorChain _ [(tfmOp, _)] : _)
  = OperatorChain e [(tfmOp, go e tfms)]
 where go e' (OperatorChain p‚ÇÄ [(tfmOp', p‚ÇÅ)] : tfms') = go (e' &~! (p‚ÇÄ:=:p‚ÇÅ)) tfms'
       go e' [] = e'


-- | Apply a sequence of pattern-transformations, each in every spot possible,
--   and yield the result
--   concatenated to the original via the corresponding chain-operator.
--   Because only the rightmost expression in a chain is processed,
--   this can be iterated, giving a chain of intermediate results.
(&~~:) :: ( Eq l, Eq (Encapsulation l), SymbolClass œÉ, SCConstraint œÉ l
         , Show (AlgebraExpr œÉ l), Show (AlgebraPattern œÉ l) )
    => AlgebraExpr œÉ l -> [AlgebraPattern œÉ l] -> AlgebraExpr œÉ l
e &~~: [] = e
OperatorChain e‚ÇÄ ((eo@(Infix (Hs.Fixity fte _) _), eŒ©):es)
     &~~: tfms@(OperatorChain p‚ÇÄ [(to@(Infix (Hs.Fixity ftp _) _),p‚ÇÅ)] : _)
   | fte<=ftp   = associativeOperator eo (OperatorChain e‚ÇÄ es) (eŒ©&~~:tfms)
e &~~: tfms@(OperatorChain _ [(tfmOp, _)] : _)
  = OperatorChain e [(tfmOp, go e tfms)]
 where go e' (OperatorChain p‚ÇÄ [(tfmOp', p‚ÇÅ)] : tfms')
          = case e' &~: (p‚ÇÄ:=:p‚ÇÅ) of
              alt -> go alt tfms'
       go e' [] = e'
